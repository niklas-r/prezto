# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.config/zsh/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...
# Set up cache dir for ZSH for certain plguins to work, such as docker and kubectl
export ZSH_CACHE_DIR=$HOME/.cache/zsh

# Setup Antidote home dir
export ANTIDOTE_HOME=$HOME/.cache/antidote

# Source Antidote package manager
source "${ZDOTDIR:-$HOME}/.antidote/antidote.zsh"

# Static antidote loading - only regenerates when plugins.txt changes
zsh_plugins="${ZDOTDIR:-$HOME}/.zsh_plugins"
if [[ ! ${zsh_plugins}.zsh -nt ${zsh_plugins}.txt ]]; then
    (
        source "${ZDOTDIR:-$HOME}/.antidote/antidote.zsh"
        antidote bundle <${zsh_plugins}.txt >${zsh_plugins}.zsh
    )
fi
source ${zsh_plugins}.zsh

# PATH party
export DOTFILES_DIR=$HOME/dotfiles
export PATH=$HOME/.bin:$PATH
# Add globally installed yarn packges to PATH
PATH=$HOME/.yarn/bin:$PATH
# BUN
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"

# Completion caching function - regenerates if older than 24h or missing
_cache_completion() {
    local cmd="$1"
    local cache_file="$HOME/.cache/zsh/completions/${cmd}.zsh"
    local gen_cmd="$2"

    # Create cache dir if needed
    [[ -d "${cache_file:h}" ]] || mkdir -p "${cache_file:h}"

    # Regenerate if missing or older than 24 hours
    if [[ ! -f "$cache_file" || $(find "$cache_file" -mtime +1 2>/dev/null) ]]; then
        eval "$gen_cmd" > "$cache_file" 2>/dev/null
    fi

    [[ -f "$cache_file" ]] && source "$cache_file"
}

# Can't escape Java 21
export PATH="/opt/homebrew/opt/openjdk@21/bin:$PATH"
export CPPFLAGS="-I/opt/homebrew/opt/openjdk@21/include"
export JAVA_HOME="/opt/homebrew/opt/openjdk@21/libexec/openjdk.jdk/Contents/Home"

# Can't escape Java
# export PATH="/opt/homebrew/opt/openjdk/bin:$PATH"
# export CPPFLAGS="-I/opt/homebrew/opt/openjdk/include"

# Config
export XDG_CONFIG_HOME=$HOME/.config

# Warp terminal
export WARP_THEMES_DIR="$HOME/.warp/themes"

# Bat
export BAT_THEME="Catppuccin Frappe"

# FZF config
export FZF_CTRL_T_OPTS="
  --preview 'bat --style numbers,changes --color=always {} | head -500'
  --bind 'f2:change-preview-window(down|hidden|)'"

# CONFIG PLUGINS
GIT_AUTO_FETCH_INTERVAL=120 # in seconds

# Aliases
alias rmorig="find . -type f -name '*.orig' -exec rm {} \;"
alias weather="curl wttr.in"

# Difftastic
export DFT_TAB_WIDTH=2

alias gdl="git -c diff.external=difft log -p --ext-diff"
alias gdsh="git -c diff.external=difft show --ext-diff"
alias gdf="git -c diff.external=difft diff"
# Same as above but with inline diffs instead of side-by-side
alias gdli="git -c diff.external='difft --display=inline' log -p --ext-diff"
alias gdshi="git -c diff.external='difft --display=inline' show --ext-diff"
alias gdfi="git -c diff.external='difft --display=inline' diff"

alias git-squashed-delete='git checkout -q master && git for-each-ref refs/heads/ "--format=%(refname:short)" | while read branch; do mergeBase=$(git merge-base master $branch) && [[ $(git cherry master $(git commit-tree $(git rev-parse "$branch^{tree}") -p $mergeBase -m _)) == "-"* ]] && git branch -D $branch; done'
alias git-squashed-delete-dry-run='git checkout -q master && git for-each-ref refs/heads/ "--format=%(refname:short)" | while read branch; do mergeBase=$(git merge-base master $branch) && [[ $(git cherry master $(git commit-tree $(git rev-parse "$branch^{tree}") -p $mergeBase -m _)) == "-"* ]] && echo "$branch is merged into master and can be deleted"; done'

alias ls='eza --group-directories-first --icons --classify'

# List tree one level deep
alias tre='ls --tree -L=1'

# List directories only
alias ld='tre -D'

# Use bat, you can call builtin cat with `\cat`
alias cat='bat'

alias resource='source ~/.zshrc'

# If you change repos in lazygit and want your shell to change directory into that repo on exiting lazygit
lg()
{
    export LAZYGIT_NEW_DIR_FILE=~/.lazygit/newdir

    lazygit "$@"

    if [ -f $LAZYGIT_NEW_DIR_FILE ]; then
        cd "$(cat $LAZYGIT_NEW_DIR_FILE)"
        rm -f $LAZYGIT_NEW_DIR_FILE > /dev/null
    fi
}

# Start Yazi and change directory if it returns a new directory
function y() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
    yazi "$@" --cwd-file="$tmp"
    if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
        builtin cd -- "$cwd"
    fi
    rm -f -- "$tmp"
}

# This allows use of ^ in command such as `git checkout HEAD^`
# https://github.com/robbyrussell/oh-my-zsh/issues/449
setopt NO_NOMATCH

# fnm - Fast Node Manager (replaces NVM)
if command -v fnm >/dev/null 2>&1; then
    _cache_completion "fnm" "fnm env --use-on-cd"
fi

# if wezterm exists, run wezterm shell-completion --shell zsh
if command -v wezterm >/dev/null 2>&1; then
    _cache_completion "wezterm" "wezterm shell-completion --shell zsh"
fi


# if gh exists, add completions
if command -v gh >/dev/null 2>&1; then
    _cache_completion "gh" "gh completion -s zsh"
fi

# Initiate zoxide (`z` command)
if command -v zoxide >/dev/null 2>&1; then
    _cache_completion "zoxide" "zoxide init zsh"
fi

# Lets testing-library print a shit tonne of DOM information when a test fails.
# Rendering SVG's in tests takes up a lot of characters.
# Default limit is 7000 characters
DEBUG_PRINT_LIMIT=20000

# On Mac OS X, cd to the path of the front Finder window
# Found at <http://brettterpstra.com/2013/02/09/quick-tip-jumping-to-the-finder-location-in-terminal>
function cdf() {
    target=$(osascript -e 'tell application "Finder" to if (count of Finder windows) > 0 then get POSIX path of (target of front Finder window as text)')
    if [ "$target" != "" ]; then
        cd "$target"
        pwd
    else
        echo 'No Finder window found' >&2
    fi
}

function dotadd() {
    local dry_run=false
    local args=()

    # Parse arguments
    while [[ $# -gt 0 ]]; do
        case $1 in
            --dry-run)
                dry_run=true
                shift
                ;;
            *)
                args+=("$1")
                shift
                ;;
        esac
    done

    # Restore positional parameters
    set -- "${args[@]}"

    if [[ -z "$1" ]]; then
        echo "Usage: dotadd [--dry-run] <source_file> [destination_name]"
        echo "Moves a file or folder to the dotfiles repo and creates a symlink"
        echo "Options:"
        echo "  --dry-run  Show what would be done without actually doing it"
        echo "Example: dotadd ~/.vimrc"
        echo "Example: dotadd --dry-run ~/.vimrc"
        return 1
    fi

    src="$1"
    dest="$HOME/dotfiles/config/${2:-$(basename $src)}"

    if [[ "$dry_run" == true ]]; then
        echo "[DRY RUN] Would move: $src -> $dest"
        echo "[DRY RUN] Would create symlink: $dest -> $src"
    else
        mv "$src" "$dest"
        ln -s "$dest" "$src"
        echo "Moved $src -> $dest and symlinked it back"
    fi
}

if command -v docker >/dev/null 2>&1; then
    _cache_completion "docker" "docker completion zsh"
fi

# Source secrets
source "$HOME/.secret-vars"

# Set up alias for GitHub Copilot CLI
# ghce - Alias for explaining command
# ghcs - Alias for suggesting command
if command -v gh >/dev/null 2>&1; then
    _cache_completion "gh-copilot" "gh copilot alias -- zsh"
fi

if command -v pnpm >/dev/null 2>&1; then
    _cache_completion "pnpm" "pnpm completion zsh"
fi

if command -v npm >/dev/null 2>&1; then
    _cache_completion "npm" "npm completion"
fi


if command -v opencode >/dev/null 2>&1; then
    _cache_completion "opencode" "opencode completion"
fi

# Disable "zsh: do you with to see all NNN possibilities?"
zstyle ':completion:*:default' list-prompt ''
zstyle ':completion:*:default' select-prompt ''
# Bind <C-i> to enter interactive menu
bindkey -M menuselect '^xi' vi-insert

if command -v moor >/dev/null 2>&1; then
    export PAGER='moor'
fi

export CARAPACE_BRIDGES='zsh,fish,bash,inshellisense' # optional
zstyle ':completion:*' format $'\e[2;37mCompleting %d\e[m'
_cache_completion "carapace" "carapace _carapace"
zstyle ':completion:*:git:*' group-order 'main commands' 'alias commands' 'external commands'

# To customize prompt, run `p10k configure` or edit ~/.config/zsh/.p10k.zsh.
[[ ! -f ~/.config/zsh/.p10k.zsh ]] || source ~/.config/zsh/.p10k.zsh

# bun completions
[ -s "/Users/niklaskahn/.bun/_bun" ] && source "/Users/niklaskahn/.bun/_bun"
